plugins {
    id 'java'
    id 'maven-publish' //plugin is applied to handle publishing artifacts
    id 'org.springframework.boot' version '3.3.1'
    id 'io.spring.dependency-management' version '1.1.5'
}

group = 'info.mastera'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
    mavenLocal()
}

ext {
    lombokVersion = '1.18.32'
    jjwtVersion = '0.12.6'
    jakartaServletApiVersion = '6.1.0'
    fasterxmlVersion = '2.17.1'
    mapstructVersion = '1.5.5.Final'
    openApiVersion = '2.6.0'
    userInfoVersion = '0.0.1-SNAPSHOT'
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:$openApiVersion"
    compileOnly "jakarta.servlet:jakarta.servlet-api:$jakartaServletApiVersion"

    implementation "io.jsonwebtoken:jjwt-api:$jjwtVersion"
    runtimeOnly "io.jsonwebtoken:jjwt-impl:$jjwtVersion"
    runtimeOnly "io.jsonwebtoken:jjwt-jackson:$jjwtVersion"

    implementation "com.fasterxml.jackson.core:jackson-databind:$fasterxmlVersion"

    compileOnly "org.projectlombok:lombok:$lombokVersion"
    annotationProcessor "org.projectlombok:lombok:$lombokVersion"

    implementation "org.mapstruct:mapstruct:$mapstructVersion"
    annotationProcessor "org.mapstruct:mapstruct-processor:$mapstructVersion"

    implementation "info.mastera:user-info:$userInfoVersion"

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

// Ensure the bootJar task does not override the jar task
bootJar {
    enabled = false
}

jar {
    enabled = true
}

//Configures the maven-publish plugin to use the jar task output for publishing.
publishing {
    publications {
        //noinspection GroovyAssignabilityCheck
        maven(MavenPublication) {
            //noinspection GroovyAssignabilityCheck
            from components.java // Specify the Java component to publish
        }
    }
    repositories {
        mavenLocal()
    }
}

tasks.named('test') {
    useJUnitPlatform()
}
